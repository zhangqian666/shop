group 'com.zack'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = 1.8

configurations {
    mybatisGenerator
}
def getDbProperties = {
    def properties = new Properties()
    file("src/main/resources/mybatis/config.properties").withInputStream { inputStream ->
        properties.load(inputStream)
    }
    properties
}

repositories {
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
    mavenCentral()
    jcenter()
}
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
task mybatisGenerate << {
    def properties = getDbProperties()
    ant.properties['targetProject'] = projectDir.path
    ant.properties['driverClass'] = properties.getProperty("jdbc.driverClassName")
    ant.properties['connectionURL'] = properties.getProperty("jdbc.url")
    ant.properties['userId'] = properties.getProperty("jdbc.username")
    ant.properties['password'] = properties.getProperty("jdbc.password")
    ant.properties['src_main_java'] = sourceSets.main.java.srcDirs[0].path
    ant.properties['src_main_resources'] = sourceSets.main.resources.srcDirs[0].path
    ant.properties['modelPackage'] = properties.getProperty("package.model")
    ant.properties['mapperPackage'] = properties.getProperty("package.mapper")
    ant.properties['sqlMapperPackage'] = properties.getProperty("package.xml")
    ant.taskdef(
            name: 'mbgenerator',
            classname: 'org.mybatis.generator.ant.GeneratorAntTask',
            classpath: configurations.mybatisGenerator.asPath
    )
    ant.mbgenerator(overwrite: true,
            configfile: 'src/main/resources/mybatis/generatorConfig.xml', verbose: true) {
        propertyset {
            propertyref(name: 'targetProject')
            propertyref(name: 'userId')
            propertyref(name: 'driverClass')
            propertyref(name: 'connectionURL')
            propertyref(name: 'password')
            propertyref(name: 'src_main_java')
            propertyref(name: 'src_main_resources')
            propertyref(name: 'modelPackage')
            propertyref(name: 'mapperPackage')
            propertyref(name: 'sqlMapperPackage')
        }
    }
}



ext {
    springVersion = "5.0.2.RELEASE"
    slf4jVersion = "1.7.21"
    log4jVersion = "1.2.17"
    jacksonVersion = "2.9.3"
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'junit:junit:4.12'
    compile 'jstl:jstl:1.2'

    //spring
    compile "org.springframework:spring-core:$springVersion"
    compile "org.springframework:spring-oxm:$springVersion"
    compile "org.springframework:spring-web:$springVersion"
    compile "org.springframework:spring-webmvc:$springVersion"
    compile "org.springframework:spring-tx:$springVersion"
    compile "org.springframework:spring-jdbc:$springVersion"
    compile "org.springframework:spring-aop:$springVersion"
    compile "org.springframework:spring-context-support:$springVersion"
    compile "org.springframework:spring-test:$springVersion"
    compile "org.springframework:spring-orm:$springVersion"

    //sql server connector
    compile 'net.sourceforge.jtds:jtds:1.3.1'

    //log
//    compile "log4j:log4j:$log4jVersion"
//    compile "org.slf4j:slf4j-log4j12:$slf4jVersion"
//    compile 'org.jboss.logging:jboss-logging:3.3.0.Final'
    //logback
    compile "ch.qos.logback:logback-classic:1.1.2"
    compile "ch.qos.logback:logback-core:1.1.2"

    //mysql
    compile 'mysql:mysql-connector-java:5.1.6'

    //mybatis
    compile "org.aspectj:aspectjweaver:1.7.3"
    compile "org.aspectj:aspectjrt:1.6.11"
    compile 'org.mybatis:mybatis-spring:1.2.3'
    compile 'org.mybatis:mybatis:3.3.0'

    //json
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.3'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.3'

    //commons
    compile "commons-dbcp:commons-dbcp:1.4"
    compile "commons-collections:commons-collections:3.2.1"

    //guava
    compile "com.google.guava:guava:20.0"
    compile "org.apache.commons:commons-lang3:3.5"
    compile "joda-time:joda-time:2.3"

    //id加密解密
    compile "org.hashids:hashids:1.0.1"

    //ftpclient
    compile "commons-net:commons-net:3.1"

    //file upload
    compile "commons-fileupload:commons-fileupload:1.2.2"
    compile "commons-io:commons-io:2.0.1"

    //mybatis page
    compile "com.github.pagehelper:pagehelper:4.1.0"
    compile "com.github.miemiedev:mybatis-paginator:1.2.17"
    compile "com.github.jsqlparser:jsqlparser:0.9.4"

    //alipay
    compile "commons-codec:commons-codec:1.10"
    compile "commons-configuration:commons-configuration:1.10"
    compile "commons-lang:commons-lang:2.6"
    compile "commons-logging:commons-logging:1.1.1"
    compile "com.google.zxing:core:2.1"
    compile "com.google.code.gson:gson:2.3.1"
    compile "org.hamcrest:hamcrest-core:1.3"
    compile "redis.clients:jedis:2.9.0"

    //mybatis generator
    mybatisGenerator 'org.mybatis.generator:mybatis-generator-core:1.3.5'
    mybatisGenerator 'mysql:mysql-connector-java:8.0.8-dmr'
    mybatisGenerator 'tk.mybatis:mapper:3.4.6'

    //include in compile only, exclude in the war
    providedCompile 'javax.servlet:servlet-api:2.5'

}
